1-AVERAGE CALORIES OF PRODUCTS FROM A GIVEN COUNTRY, IN DESCENDING ORDER, COUNTRY MUST HAVE AT LEAST 5 PRODUCTS (TO EXCLUDE OUTLIERS)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?country (AVG (?kcal) as ?avgKcal) (count(?product) as ?n_product) where {
    ?product rdf:type :Product.
    ?product :fromCountry ?country.
    ?product :energyKcal_100g ?kcal
}
group by ?country
having (?n_product > 5)
order by desc(?avgKcal)


2-LIST OF PRODUCTS ORDERED BY HOW MANY INGREDIENTS THEY HAVE AND A COLUMN TO SHOW HOW MANY OF THEM ARE ADDITIVES

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?product ?n_ingredient (count(?additive) as ?n_additives) where {
    ?product :hasIngredient ?additive.
    ?additive rdf:type :Additive.
    { 
	select ?product (count(?ingredient) as ?n_ingredient) where {
    	?product rdf:type :Product.
    	?product :hasIngredient ?ingredient.   
            
	}group by ?product
	order by desc(?n_ingredient)
        limit 100}
    
}group by ?product ?n_ingredient


3-LIST OF MOST USED PACKAGING TAGS

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?package (count(?package) as ?n_packages) where {
  ?product rdf:type :Product;
           :hasPackaging ?package.
  ?package rdf:type :Packaging.  
}group by ?package
order by desc(?n_packages)


4-LIST OF FOOD GROUPS WITH THE AVERAGE NUMBER OF CALORIES FOR PRODUCTS BELONGING TO THAT GROUP, IN DESCENDANT ORDER

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?foodgroup (avg(?kcal) as ?avg_kcal) where {
  ?product rdf:type :Product;
           :belongsToGroup ?foodgroup;
           :energyKcal_100g ?kcal.
    
  ?foodgroup rdf:type :FoodGroup.
  
}group by ?foodgroup
order by desc(?avg_kcal)


5-DESCRIPTION OF A SPECIFIED PRODUCT (CINNAMON WAFFLES) :)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
describe ?product where {
  ?product rdf:type :Product.
  ?product :name "Cinnamon waffles"^^xsd:string
}


6-AVERAGE NUMBER OF CALORIES FOR PRODUCTS THAT CONTAIN PALM OIL (AROUND 400, FOR COMPARISON GLOBAL AVERAGE IS 270 AND THE AVERAGE FOR PRODUCTS WITH OLIVE OIL IS 290). 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select (avg(?kcal_palm) as ?avg_kcal_with_palm_oil) where {
    ?product rdf:type :Product;
             :energyKcal_100g ?kcal_palm;
             :hasIngredient ?ingredient.
    ?ingredient :ingredientName ?ingr_n.
    filter regex(?ingr_n, "palm-oil*")    
} 


7-FOR EVERY ECOSCORE GRADE, THE NUMBER OF PRODUCTS THAT HAVE THAT GRADE AND HAVE PLASTIC IN THE PACKAGING LIST (THERE ARE NO PRODUCTS IN "A" CATEGORY)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?ecoscore (count(?ecoscore) as ?n_ecoscore_with_plastic) where {
    ?product :hasPackaging :plastic;
             :ecoscoreGrade ?ecoscore.     
}group by ?ecoscore
order by ?ecoscore


8-AVERAGE VALUE OF CALORIES FOR PRODUCTS THAT HAVE LABEL "ORGANIC" (AROUND 280, GLOBAL AVERAGE IS 270)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select (avg(?kcal) as ?avg_kcal_organic) where {
    ?product rdf:type :Product;
             :hasLabel :organic;
             :energyKcal_100g ?kcal.
    
}


9-FOR EVERY NUTRISCORE GRADE, HOW MANY FIBERS HAVE ON AVERAGE THE PRODUCTS WITH THAT GRADE

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?nutriscore (avg(?fiber) as ?avg_fiber) where {
    ?product rdf:type :Product;
             :nutriscoreGrade ?nutriscore;
             :fiber_100g ?fiber.
    
}group by ?nutriscore
order by ?nutriscore


10-LIST OF BRANDS WITH THE AVERAGE VALUE UF THE NUTRISCORE SCORE OF THEIR PRODUCTS (THE LOWER VALUE, THE HEALTIER)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.graphle.com/foodOntology#>

select ?brand_n (avg(?nutriscore) as ?avg_nutriscore) where {
    ?product rdf:type :Product;
             :nutriscoreScore ?nutriscore;
             :hasBrand ?brand.
    ?brand rdf:type :Brand;
           :name ?brand_n.
    
    
}group by ?brand_n
order by desc(?avg_nutriscore)